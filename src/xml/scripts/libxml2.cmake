include (iconv)

set (LIBXML2_SUPPORT_DEFAULT ON)
if (UNIX)
 find_package (LibXml2 MODULE)
 if (LibXml2_FOUND)
  message (STATUS "found LibXml2")
  set (LIBXML2_FOUND TRUE)
 endif (LibXml2_FOUND)
# pkg_check_modules (PKG_LIBXML2 REQUIRED libxml-2.0)
elseif (WIN32)
 if (VCPKG_SUPPORT)
  find_package (LibXml2 MODULE)
  if (LibXml2_FOUND)
   message (STATUS "found LibXml2")
   set (LIBXML2_FOUND TRUE)
   if (CMAKE_BUILD_TYPE STREQUAL "Debug" OR
       CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo")
    set (LIBXML2_LIB_DIR "${VCPKG_ROOT}/installed/${VCPKG_TARGET_TRIPLET}/debug/bin")
   else ()
    set (LIBXML2_LIB_DIR "${VCPKG_ROOT}/installed/${VCPKG_TARGET_TRIPLET}/bin")
   endif ()
  endif (LibXml2_FOUND)
 endif (VCPKG_SUPPORT)
 if (NOT LIBXML2_FOUND)
  find_path (LIBXML2_INCLUDE_DIR NAMES libxml/xpath.h
             PATHS "$ENV{LIB_ROOT}/libxml2/include"
             PATH_SUFFIXES libxml2)
  set (CMAKE_FIND_LIBRARY_SUFFIXES .lib .dll.a)
  find_library (LIBXML2_LIBRARY
                NAMES libxml2
                PATHS "$ENV{LIB_ROOT}/libxml2"
                PATH_SUFFIXES lib
                NO_DEFAULT_PATH)
  if (LIBXML2_INCLUDE_DIR AND LIBXML2_LIBRARY)
   message (STATUS "found libxml2")
   set (LIBXML2_FOUND TRUE)
   set (LIBXML2_INCLUDE_DIRS "${LIBXML2_INCLUDE_DIR}")
   set (LIBXML2_LIBRARIES "${LIBXML2_LIBRARY}")
   set (LIBXML2_LIB_DIR "$ENV{LIB_ROOT}/libxml2/bin")
  else ()
   message (WARNING "could not find libxml2, continuing")
  endif (LIBXML2_INCLUDE_DIR AND LIBXML2_LIBRARY)
 endif (NOT LIBXML2_FOUND)
endif ()
if (LIBXML2_FOUND AND LIBICONV_SUPPORT)
 option (LIBXML2_SUPPORT "enable libxml2 support" ${LIBXML2_SUPPORT_DEFAULT})
 if (LIBXML2_SUPPORT)
  add_definitions (-DLIBXML2_SUPPORT)
  set (LIBXML2_INCLUDE_DIRS "${LIBICONV_INCLUDE_DIRS};${LIBXML2_INCLUDE_DIRS}")
  set (LIBXML2_LIBRARIES "${LIBICONV_LIBRARIES};${LIBXML2_LIBRARIES}")
  set (LIBXML2_LIB_DIR "${LIBICONV_LIB_DIR};${LIBXML2_LIB_DIR}")
 endif (LIBXML2_SUPPORT)
endif (LIBXML2_FOUND AND LIBICONV_SUPPORT)
